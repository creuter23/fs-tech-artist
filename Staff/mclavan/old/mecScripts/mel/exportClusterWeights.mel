// Copyright (C) 2000-2002 Michael Bazhutkin
// www.geocites.com/bazhutkin
// bazhutkin@mail.ru
//
//  Export Cluster Weights 1.0
//  Script File
//  MODIFY THIS AT YOUR OWN RISK
//
//	Creation Date:	November 5, 2002
//	Fix Date:	November 19, 2002
//
//  Description:
//	Export cluster weights of selected object into file
//
//	Select object (polygon or NURBS)
//	and cluster node in Channel Box
//	if cluster deformer is not single for this object
//
//	Call exportClusterWeights

global proc exportClusterWeightsGetFileName ( string $fileName, string $fileType )
{
	global string $exportClusterWeightsFileName;
	$exportClusterWeightsFileName = $fileName;
}

global proc int exportClusterWeights ( )
{
	global string $exportClusterWeightsFileName;
	
	string $list[] = `ls -sl -type transform`;
	string $objectName = $list[0];
	
	if (size($list) < 1) error ("No selected object");
	if (size($list) > 1) error ("More than one object selected");
	
	$list = `ls -sl -type cluster`;
	
	if (size($list) < 1)
	{
		$list = `listHistory $objectName`;
		$list = `ls -type cluster $list`;
	}
	
	string $clusterName = $list[0];
	
	if (size($list) < 1) error ("No cluster deformer for selected object");
	if (size($list) > 1) error ("More than one cluster deformer for selected object. Select cluster node in Channel Box.");
	
	string $shapeName[] = `ls -dag -s $objectName`;
	string $history[] = `listHistory $objectName`;
	string $clustersName[] = `ls -type cluster $history`;
	string $clusterSetName[] = `listConnections -type objectSet $clusterName`;
	
	int $flag;
	for ($clusterMember in $clustersName)
		if ($clusterMember == $clusterName) $flag++;
	if ($flag == 0) error ("`" + $clusterName + "` is not deformer for `" + $objectName + "`");
	
	int $sizeInputArr = `getAttr -size ($clusterName + ".input")`;
	
	int $indexInputArr;
	string $input[];
	string $future[];
	for ($i = 0; $i < $sizeInputArr; $i++)
	{
		$input = `listConnections ($clusterName + ".input[" + $i + "].inputGeometry")`;
		if ($input[0] != "")
		{	$future = `listHistory -future 1 $input[0]`;
			for ($node in $future)
				if ($shapeName[0] == $node) $indexInputArr = $i;
		}
	}
	
	// Get file name
	
	$exportClusterWeightsFileName = "";
	fileBrowser ("exportClusterWeightsGetFileName", "Export", "", 1);
	
	if ($exportClusterWeightsFileName == "") return 0;
	
	string $filePartName[];
	string $fileExtName = ".weights";
	
	tokenize $exportClusterWeightsFileName "." $filePartName;
	string $fileName = $filePartName[0] + $fileExtName;
	
	// Export weights
		
	waitCursor -state 1;
	
	$fileId = `fopen $fileName "w"`;
	if ($fileId == 0)
		error("Unable to open the file: " + $fileName);
	
	fprint $fileId "N\tWeights\r\n\r\n";
	
	int $sizeArray = `getAttr -size ($objectName + ".cp")`;
	float $pointValue[] = `getAttr ($clusterName + ".weightList[" + $indexInputArr + "].w[0:" + ($sizeArray - 1) + "]")`;
	
	for ($i = 0; $i < $sizeArray; $i++)
		if (`sets -im $clusterSetName[0] ($objectName + ".cp[" + $i + "]")`)
			fprint $fileId ($i + "\t" + $pointValue[$i] + "\r\n");
		
	fclose $fileId;
	
	waitCursor -state 0;
	
	return 1;
}